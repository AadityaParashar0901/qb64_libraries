Sub RePERM (SEED As _Unsigned Integer)
    Randomize SEED
    For I = 0 To 255
        For J = 0 To 255
            For K = 0 To 7
                perm2(I, J, K) = Rnd
    Next K, J, I
    Randomize SEED
    For I = 0 To 15: For J = 0 To 15: For K = 0 To 15: For L = 0 To 7
                    perm3(I, J, K, L) = Rnd
    Next L, K, J, I
End Sub
Function interpolate! (A!, B!, C!)
    interpolate! = A! + (B! - A!) * C!
End Function
Function fractal2! (X As Long, Y As Long, S As _Unsigned Long, O As _Unsigned _Byte, M As _Unsigned _Byte) Static
    Dim As Double amplitude, total, maxAmplitude, scale
    Dim As Single tX, tY
    Dim As _Byte i
    $Checking:Off
    amplitude = 1: total = 0: maxAmplitude = 0: scale = S
    For i = 0 To O
        tX = X / scale
        tY = Y / scale
        fX& = Int(tX)
        fY& = Int(tY)
        dX~%% = (tX - fX&) * 256
        dY~%% = (tY - fY&) * 256
        fX~%% = fX& And 255
        fY~%% = fY& And 255
        fX1~%% = fX~%% + 1
        fY1~%% = fY~%% + 1
        n00! = perm2(fX~%%, fY~%%, M)
        n10! = perm2(fX1~%%, fY~%%, M)
        n01! = perm2(fX~%%, fY1~%%, M)
        n11! = perm2(fX1~%%, fY1~%%, M)
        i1! = n00! + (n10! - n00!) * fade!(dX~%%)
        i2! = n01! + (n11! - n01!) * fade!(dX~%%)
        total = total + amplitude * (i1! + (i2! - i1!) * fade!(dY~%%))
        maxAmplitude = maxAmplitude + amplitude
        amplitude = amplitude / 2
        scale = scale / 2
    Next i
    $Checking:On
    fractal2! = total / maxAmplitude
End Function
Function fractal3! (X As Long, Y As Long, Z As Long, S As _Unsigned Long, O As _Unsigned _Byte, M As _Unsigned _Byte) Static
    Dim As Double amplitude, total, maxAmplitude, scale
    Dim As Single tX, tY, tZ
    Dim As _Byte i
    $Checking:Off
    amplitude = 1: total = 0: maxAmplitude = 0: scale = S
    For i = 0 To O
        tX = X / scale
        tY = Y / scale
        tZ = Z / scale
        fX& = Int(tX)
        fY& = Int(tY)
        fZ& = Int(tZ)
        dX~%% = (tX - fX&) * 256
        dY~%% = (tY - fY&) * 256
        dZ~%% = (tZ - fZ&) * 256
        fX~`4 = fX& And 15
        fY~`4 = fY& And 15
        fZ~`4 = fZ& And 15
        fX1~`4 = fX~`4 + 1
        fY1~`4 = fY~`4 + 1
        fZ1~`4 = fZ~`4 + 1
        n000! = perm3(fX~`4, fY~`4, fZ~`4, M)
        n100! = perm3(fX1~`4, fY~`4, fZ~`4, M)
        n010! = perm3(fX~`4, fY1~`4, fZ~`4, M)
        n110! = perm3(fX1~`4, fY1~`4, fZ~`4, M)
        n001! = perm3(fX~`4, fY~`4, fZ1~`4, M)
        n101! = perm3(fX1~`4, fY~`4, fZ1~`4, M)
        n011! = perm3(fX~`4, fY1~`4, fZ1~`4, M)
        n111! = perm3(fX1~`4, fY1~`4, fZ1~`4, M)
        i01! = n000! + (n100! - n000!) * fade!(dX~%%)
        i02! = n010! + (n110! - n010!) * fade!(dX~%%)
        i11! = n001! + (n101! - n001!) * fade!(dX~%%)
        i12! = n011! + (n111! - n011!) * fade!(dX~%%)
        n1! = i01! + (i02! - i01!) * fade!(dY~%%)
        n2! = i11! + (i12! - i11!) * fade!(dY~%%)
        total = total + amplitude * (n1! + (n2! - n1!) * fade!(dZ~%%))
        maxAmplitude = maxAmplitude + amplitude
        amplitude = amplitude / 2
        scale = scale / 2
    Next i
    $Checking:On
    fractal3! = total / maxAmplitude
End Function
